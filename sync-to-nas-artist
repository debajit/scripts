#!/usr/bin/env zsh

#
# sync-to-nas-artist - Sync music artist to NAS
#
# DESCRIPTION
#   Syncs the artist directory from the local disk to the local NAS,
#   deleting files from the NAS if required.
#
# OPTIONS
#   -n, --dry-run
#     Does a dry-run and shows what file changes will be made
#
# EXAMPLES
#   sync-to-nas-artist 'Rammstein' --dry-run
#   sync-to-nas-artist -n 'Jonita Gandhi/'
#   sync-to-nas-artist 'Joe Satriani'
#
# NOTE

#  - Trailing slashes in the artist directory do not matter (they are
#    all removed).

#  - I use zsh as the script runner here to be able to use the
#    ${var:a} construct to remove all trailing slashes (The bash
#    construct ${var%/} only removes one trailing slash).
#

set -euo pipefail

source "notification.sh"

# TODO: Refactor into a common error handling sourceable script
die() { echo_error "${1}" ; exit 1; }

dry_run=0
artist=""

while [[ -n "${1:-}" ]]; do
  case "${1}" in
    -n|--dry-run)
      dry_run=1
      ;;
    -*)
      echo_error "Invalid option '${1}'"
      exit 1
      ;;
    *)
      artist="${1:a}"
      # artist="${1%/}"
      ;;
  esac

  shift
done

[[ -d "${artist}" ]] || die "Artist directory '${artist}' does not exist"

if [[ $dry_run == 1 ]]; then
  echo_notify "Dry-syncing artist '${artist}'..."
  rsync -sazvhP --delete-after --exclude-from="$HOME/.rsyncexcludes" "${artist}" "nas:/volume1/Music" -ni
else
  echo_notify "Syncing artist '${artist}' to nas"
  rsync -sazvhP --delete-after --exclude-from="$HOME/.rsyncexcludes" "${artist}" "nas:/volume1/Music"
fi
